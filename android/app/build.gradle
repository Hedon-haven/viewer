plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}

// Load debug keystore properties
def keystorePropertiesDebug = new Properties()
def keystorePropertiesFileDebug = rootProject.file("keystores/debug.properties")
if (keystorePropertiesFileDebug.exists()) {
    keystorePropertiesDebug.load(new FileInputStream(keystorePropertiesFileDebug))
}

// Load release keystore properties
def keystorePropertiesRelease = new Properties()
def keystorePropertiesFileRelease = rootProject.file("keystores/release.properties")
if (keystorePropertiesFileRelease.exists()) {
    keystorePropertiesRelease.load(new FileInputStream(keystorePropertiesFileRelease))
}

android {
    namespace = "com.hedon_haven.viewer"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.hedon_haven.viewer"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }

    signingConfigs {
        debug {
            keyAlias keystorePropertiesDebug['keyAlias']
            keyPassword keystorePropertiesDebug['keyPassword']
            storeFile keystorePropertiesDebug['storeFile'] ? file(keystorePropertiesDebug['storeFile']) : null
            storePassword keystorePropertiesDebug['storePassword']
        }
        release {
            keyAlias keystorePropertiesRelease['keyAlias']
            keyPassword keystorePropertiesRelease['keyPassword']
            storeFile keystorePropertiesRelease['storeFile'] ? file(keystorePropertiesRelease['storeFile']) : null
            storePassword keystorePropertiesRelease['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "Hedon haven debug"]
        }
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "Hedon haven"]
        }

        profile {
            applicationIdSuffix ".profile"
            manifestPlaceholders = [appName: "Hedon haven profile"]
        }
    }
}

flutter {
    source = "../.."
}
